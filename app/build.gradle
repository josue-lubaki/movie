plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.kursatkumsuz.movie'
    compileSdk ProjectConfig.compileSdk

    defaultConfig {
        applicationId "com.kursatkumsuz.movie"
        minSdk ProjectConfig.minSdk
        targetSdk ProjectConfig.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())

        buildConfigField "String", "API_KEY", "\"${properties.getProperty("API_KEY")}\""

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion ProjectConfig.kotlinCompilerExtensionVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation libs.android.core.ktx
    implementation libs.lifecycle.runtime.ktx
    implementation libs.activity.compose
    implementation libs.compose.ui
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material
    implementation libs.compose.ui.util

    // Data Store
    implementation libs.datastore.preferences

    // Dependency Injection - Hilt
    kapt libs.dagger.hilt.compiler
    implementation libs.hilt.navigation.compose
    implementation libs.hilt.android

    // Coroutines
    implementation libs.coroutine.android
    implementation libs.coroutine.play.services

    implementation libs.lifecycle.viewmodel
    implementation libs.lifecycle.viewmodel.ktx

    // Animated Navigation
    implementation libs.animated.navigation.compose

    // Pager Indicator
    implementation libs.accompanist.pager
    implementation libs.accompanist.pager.indicator

    //Firebase
    implementation libs.firebase.auth
    implementation libs.firebase.firestore


    // Animated Message Bar
    implementation libs.kursat.animated.message.bar

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.converter

    // Paging 3
    implementation libs.paging.runtime
    implementation libs.paging.compose

    // Coil
    implementation libs.coil

}

kapt {
    correctErrorTypes true
}